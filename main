; Duet 12864 Menu Files
;
; This is a set of menu files for running a 12864 display with a duet3D control board.
;
; Written by Jadon Miller - 2/27/2019
; Partially adapted from the work of tinkerlifeprojects on Github.
;
; File "main"
;
; This is the file for the overview screen of the menu system.
; On the top of the screen we have a menu that updates based
; on the print status. In the middle we show the temperatures and fan
; speeds, and the bottom shows the percentage complete and time left according
; to filament usage, or the axis positions if you're not in a print. There 
; is a scroll button on the right to go to the "altMain" menu.



; --------------- "Not Printing" Menu Buttons ---------------
button R0 C0 F0 V5 W42 T"Print" A"menu selectSD"
image V5 L"imgs/vline.img"
button V5 W42 T"Move" A"menu moveAxis"
image V5 L"imgs/vline.img"
button V5 W42 T"Menu" A"menu mainMenu"


; --------------- "Printing" Menu Buttons ---------------
button R0 C0 F0 W64 V2 T"Pause" A"M25"
image V2 L"imgs/vline.img"
button W64 V2 T"Info" A"menu printInfo"


; --------------- "Paused" Menu Buttons ---------------
button R0 C0 F0 W42 V6 T"Resume" A"M24"
image V6 L"imgs/vline.img"
button W42 V6 T"Menu" A"menu mainMenu"
image V6 L"imgs/vline.img"
button W42 V6 T"Abort" A"menu confirmAbort"


; --------------- Main Overview ---------------
; Note: this section may need modified if you use different heater and fan numbers.
image R11 C0 L"imgs/hline.img"

; Line 1: Bed Temperature
image R14 C10 L"imgs/bed.img"
alter C25 N180 W20 H0
text T"/"
value N80 W20
button C70 W22 T"Off" A"M140 S0"

; Line 2: Hotend Temperature
image R26 C10 L"imgs/temp.img"
alter C25 N100 W20 H0
text T"/"
value N0 W20
button C70 W22 T"Off" A"M568 P0 R0 S0 A0"

; Line 3: Fan Speed and Flow Rate
image R38 C10 L"imgs/fan.img"
alter C25 N300 W25 H1
image C65 L"imgs/flowrate.img"
alter C80 N400 W25 H2

; Line 4: Print Speed and Babystepping
image R50 C10 L"imgs/feedrate.img"
alter C25 N500 W25 H2
image C65 L"imgs/zstep.img"
alter C80 D2 N521 W25 H2
